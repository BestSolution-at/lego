/*******************************************************************************
 * Copyright (c) 2014 BestSolution.at and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GPL v3
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl-3.0.txt
 *
 * Contributors:
 *     Tom Schindl<tom.schindl@bestsolution.at> - initial API and implementation
 *******************************************************************************/
grammar at.bestsolution.lego.Lego with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate lego "http://www.bestsolution.at/lego/Lego"

Model:
	repo=ItemRepository | assembly=Assembly
;

ValidID:
	ID;

QualifiedName:
  ValidID (=>'.' ValidID)*;

ItemRepository:
	name=ValidID '{' 
		elementList+=RepostoryItem+ 
	'}'
;

LegoElement:
	RepostoryItem | Assembly
;

RasterItem:
	Brick | Assembly
;

MountedPart:
	'mpart' name=ValidID fill=Color source=Source
;

RepostoryItem:
	Brick | MountedPart
;

Color:
	'rgb' r=INT g=INT b=INT
;

XUnit:
	units=INT
;

YUnit:
	units=INT
;

ZUnit:
	units=INT
;

Brick:
	'brick' name=ValidID 'units' xUnits=XUnit yUnits=YUnit zUnits=ZUnit fill=Color source=Source
;

Source:
	Generated | FxmlInclude
;

Generated:
	'generated' ( 'box' width=FLOAT height=FLOAT depth=FLOAT ('origin' originX=FLOAT originY=FLOAT originZ=FLOAT)? )? 
;

FxmlInclude:
	'fxml' source3d=STRING ('origin' originX=FLOAT originY=FLOAT originZ=FLOAT)?
;

Assembly:
	'assembly' name=ValidID '{'
		items+=AssemblyItem+
	'}'
;

AssemblyItem:
	RasterAssemblyItem | MountedAssemblyItem
;

RasterAssemblyItem:
	element=[RasterItem|QualifiedName] 'pos' xUnits=XUnit yUnits=YUnit zUnits=ZUnit 
		( 'transform' transform=('rotate180'|'rotate90' | 'rotate270') )?
;

MountedAssemblyItem:
	element=[RasterItem|QualifiedName] 'pos' xUnits=FLOAT yUnits=FLOAT zUnits=FLOAT 
		( 'transform' rotateX=FLOAT rotateY=FLOAT rotateZ=FLOAT  )?	
;

terminal INT returns ecore::EInt:
	('-')? ('0'..'9')+
;
terminal FLOAT returns ecore::EFloat:
	('-')? ('0'..'9')+ '.' ('0'..'9')*
;